{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Portfolio trading strategy example\n",
    "\n",
    "This example demonstrates how to run a backtest with multiple assets, i.e.\n",
    "trading a portfolio of assets.\n",
    "\n",
    "The price data is loaded from a CSV file containing daily close prices for\n",
    "the stocks AAPL, NVDA, TSLA, and GE, ranging from 2022-01-03 to 2024-04-22.\n",
    "\n",
    "The strategy buys one stock if the last 5 days were positive,\n",
    "and sells it again if the last 2 days were negative.\n",
    "Each trade is executed at a commission of 0.1%.\n",
    "\n",
    "When missing data points are detected for a stock,\n",
    "all open positions for that stock are closed.\n",
    "Logic of this type is common in real-world strategies\n",
    "and harder to implement in a vectorized way,\n",
    "showcasing the flexibility of Fastback.\n",
    "\n",
    "The account equity, balance and drawdowns are collected for\n",
    "every day and plotted at the end using the Plots package.\n",
    "Additionally, the performance and P&L breakdown of each stock is plotted\n",
    "and statistics (avg. P&L, worst P&L, best P&L, win rate) are printed."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━ ACCOUNT SUMMARY ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━\n",
      "\u001b[1mCash balances\u001b[0m (1)\n",
      "┌─────┬──────────┐\n",
      "│     │    Value │\n",
      "├─────┼──────────┤\n",
      "│ USD │ 13490.65 │\n",
      "└─────┴──────────┘\n",
      "\n",
      "\u001b[1mEquity balances\u001b[0m (1)\n",
      "┌─────┬──────────┐\n",
      "│     │    Value │\n",
      "├─────┼──────────┤\n",
      "│ USD │ 13490.65 │\n",
      "└─────┴──────────┘\n",
      "\n",
      "\u001b[1mPositions\u001b[0m (0)\n",
      "\n",
      "\u001b[1mTrades\u001b[0m (76)\n",
      "┌────┬────────┬─────────────────────┬────────┬────────┬────────┬────┬────┬──────\n",
      "│ ID │ Symbol │                Date │    Qty │ Filled │  Price │ TP │ SL │ Ccy ⋯\n",
      "├────┼────────┼─────────────────────┼────────┼────────┼────────┼────┼────┼──────\n",
      "│  1 │     GE │ 2022-02-02 21:00:00 │  26.32 │  26.32 │  75.98 │  — │  — │ USD ⋯\n",
      "│  2 │     GE │ 2022-02-09 21:00:00 │ -26.32 │ -26.32 │  76.91 │  — │  — │ USD ⋯\n",
      "│  3 │     GE │ 2022-03-14 20:00:00 │  27.95 │  27.95 │  71.71 │  — │  — │ USD ⋯\n",
      "│  4 │   AAPL │ 2022-03-21 20:00:00 │  12.34 │  12.34 │ 163.51 │  — │  — │ USD ⋯\n",
      "│  5 │   NVDA │ 2022-03-21 20:00:00 │   7.54 │   7.54 │ 267.02 │  — │  — │ USD ⋯\n",
      "│  6 │   TSLA │ 2022-03-21 20:00:00 │   6.56 │   6.56 │ 307.05 │  — │  — │ USD ⋯\n",
      "│  7 │   NVDA │ 2022-03-23 20:00:00 │  -7.54 │  -7.54 │ 256.03 │  — │  — │ USD ⋯\n",
      "│  8 │     GE │ 2022-03-28 20:00:00 │ -27.95 │ -27.95 │  71.36 │  — │  — │ USD ⋯\n",
      "│  9 │   AAPL │ 2022-03-31 20:00:00 │ -12.34 │ -12.34 │ 172.64 │  — │  — │ USD ⋯\n",
      "│ 10 │   TSLA │ 2022-03-31 20:00:00 │  -6.56 │  -6.56 │ 359.20 │  — │  — │ USD ⋯\n",
      "│ 11 │   AAPL │ 2022-07-08 20:00:00 │  14.24 │  14.24 │ 145.59 │  — │  — │ USD ⋯\n",
      "│ 12 │   NVDA │ 2022-07-19 20:00:00 │  12.28 │  12.28 │ 169.75 │  — │  — │ USD ⋯\n",
      "│ 13 │   TSLA │ 2022-07-19 20:00:00 │   8.49 │   8.49 │ 245.53 │  — │  — │ USD ⋯\n",
      "│ 14 │     GE │ 2022-07-21 20:00:00 │  40.05 │  40.05 │  52.91 │  — │  — │ USD ⋯\n",
      "│ 15 │   AAPL │ 2022-07-25 20:00:00 │ -14.24 │ -14.24 │ 151.44 │  — │  — │ USD ⋯\n",
      "│  ⋮ │      ⋮ │                   ⋮ │      ⋮ │      ⋮ │      ⋮ │  ⋮ │  ⋮ │   ⋮ ⋱\n",
      "│ 62 │     GE │ 2023-11-22 21:00:00 │ -21.25 │ -21.25 │ 119.52 │  — │  — │ USD ⋯\n",
      "│ 63 │   NVDA │ 2023-11-22 21:00:00 │  -5.26 │  -5.26 │ 487.12 │  — │  — │ USD ⋯\n",
      "│ 64 │   AAPL │ 2023-11-27 21:00:00 │ -13.58 │ -13.58 │ 189.55 │  — │  — │ USD ⋯\n",
      "│ 65 │   NVDA │ 2023-12-18 21:00:00 │   4.97 │   4.97 │ 500.77 │  — │  — │ USD ⋯\n",
      "│ 66 │   NVDA │ 2023-12-20 21:00:00 │  -4.97 │  -4.97 │ 481.11 │  — │  — │ USD ⋯\n",
      "│ 67 │     GE │ 2024-01-10 21:00:00 │  19.03 │  19.03 │ 129.70 │  — │  — │ USD ⋯\n",
      "│ 68 │   NVDA │ 2024-01-10 21:00:00 │   4.54 │   4.54 │ 543.50 │  — │  — │ USD ⋯\n",
      "│ 69 │     GE │ 2024-01-16 21:00:00 │ -19.03 │ -19.03 │ 127.97 │  — │  — │ USD ⋯\n",
      "│ 70 │     GE │ 2024-02-13 21:00:00 │  18.50 │  18.50 │ 141.77 │  — │  — │ USD ⋯\n",
      "│ 71 │   NVDA │ 2024-02-16 21:00:00 │  -4.54 │  -4.54 │ 726.13 │  — │  — │ USD ⋯\n",
      "│ 72 │   NVDA │ 2024-03-06 21:00:00 │   3.03 │   3.03 │ 887.00 │  — │  — │ USD ⋯\n",
      "│ 73 │   NVDA │ 2024-03-11 20:00:00 │  -3.03 │  -3.03 │ 857.74 │  — │  — │ USD ⋯\n",
      "│ 74 │   NVDA │ 2024-03-22 20:00:00 │   2.89 │   2.89 │ 942.89 │  — │  — │ USD ⋯\n",
      "│ 75 │     GE │ 2024-03-25 20:00:00 │ -18.50 │ -18.50 │ 173.49 │  — │  — │ USD ⋯\n",
      "│ 76 │   NVDA │ 2024-03-27 20:00:00 │  -2.89 │  -2.89 │ 902.50 │  — │  — │ USD ⋯\n",
      "└────┴────────┴─────────────────────┴────────┴────────┴────────┴────┴────┴──────\n",
      "                                                   4 columns and 46 rows omitted\n",
      "━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "using Fastback\n",
    "using Dates\n",
    "using CSV\n",
    "using DataFrames\n",
    "\n",
    "data_path = \"../data/stocks_1d.csv\";\n",
    "\n",
    "# if data path doesn't exist, try to change working directory\n",
    "isfile(data_path) || cd(\"src/examples\")\n",
    "\n",
    "# load CSV daily stock data for symbols AAPL, NVDA, TSLA, GE\n",
    "df_csv = DataFrame(CSV.File(data_path; dateformat=\"yyyy-mm-dd HH:MM:SS\"));\n",
    "df_csv.symbol = Symbol.(df_csv.symbol); # convert string to symbol type\n",
    "df = unstack(df_csv, :dt_close, :symbol, :close) # pivot long to wide format\n",
    "symbols = Symbol.(names(df)[2:end]);\n",
    "\n",
    "# print summary\n",
    "describe(df)\n",
    "\n",
    "# create trading account with $10'000 start capital\n",
    "acc = Account(; mode=AccountMode.Margin, base_currency=:USD);\n",
    "deposit!(acc, Cash(:USD), 10_000.0);\n",
    "\n",
    "# register instruments for all symbols\n",
    "instruments = map(sym -> Instrument(sym, sym, :USD; margin_mode=MarginMode.PercentNotional), symbols);\n",
    "register_instrument!.(Ref(acc), instruments);\n",
    "\n",
    "# data collector for account balance, equity and drawdowns (sampling every day)\n",
    "collect_balance, balance_data = periodic_collector(Float64, Day(1));\n",
    "collect_equity, equity_data = periodic_collector(Float64, Day(1));\n",
    "collect_drawdown, drawdown_data = drawdown_collector(DrawdownMode.Percentage, Day(1));\n",
    "\n",
    "function open_position!(acc, inst, dt, price)\n",
    "    # invest 20% of equity in the position\n",
    "    qty = 0.2equity(acc, :USD) / price\n",
    "    order = Order(oid!(acc), inst, dt, price, qty)\n",
    "    fill_order!(acc, order; dt=dt, fill_price=price, bid=price, ask=price, last=price, commission_pct=0.001)\n",
    "end\n",
    "\n",
    "function close_position!(acc, inst, dt, price)\n",
    "    # close position for instrument, if any\n",
    "    pos = get_position(acc, inst)\n",
    "    has_exposure(pos) || return\n",
    "    order = Order(oid!(acc), inst, dt, price, -pos.quantity)\n",
    "    fill_order!(acc, order; dt=dt, fill_price=price, bid=price, ask=price, last=price, commission_pct=0.001)\n",
    "end\n",
    "\n",
    "# loop over each row of DataFrame\n",
    "for i in 6:nrow(df)\n",
    "    row = df[i, :]\n",
    "    dt = row.dt_close\n",
    "\n",
    "    # loop over all instruments and check strategy rules\n",
    "    for inst in instruments\n",
    "        price = row[inst.symbol]\n",
    "\n",
    "        window_open = @view df[i-5:i, inst.symbol]\n",
    "        window_close = @view df[i-2:i, inst.symbol]\n",
    "\n",
    "        # close position of instrument if missing data\n",
    "        if any(ismissing.(window_open))\n",
    "            close_price = get_position(acc, inst).avg_entry_price\n",
    "            close_position!(acc, inst, dt, close_price)\n",
    "            continue\n",
    "        end\n",
    "\n",
    "        if !is_exposed_to(acc, inst)\n",
    "            # buy if last 5 days were positive\n",
    "            all(diff(window_open) .> 0) && open_position!(acc, inst, dt, price)\n",
    "        else\n",
    "            # close position if last 2 days were negative\n",
    "            all(diff(window_close) .< 0) && close_position!(acc, inst, dt, price)\n",
    "        end\n",
    "\n",
    "        # update position and account P&L\n",
    "        update_marks!(acc, inst, dt, price, price, price)\n",
    "    end\n",
    "\n",
    "    # close all positions at the end of backtest\n",
    "    if i == nrow(df)\n",
    "        for inst in instruments\n",
    "            price = row[inst.symbol]\n",
    "            close_position!(acc, inst, dt, price)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # collect data for plotting\n",
    "    if should_collect(equity_data, dt)\n",
    "        equity_value = equity(acc, :USD)\n",
    "        collect_balance(dt, cash_balance(acc, :USD))\n",
    "        collect_equity(dt, equity_value)\n",
    "        collect_drawdown(dt, equity_value)\n",
    "    end\n",
    "end\n",
    "\n",
    "# print account summary\n",
    "show(acc)"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Plot account balance, equity, drawdowns and stocks performance"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Plots, Query, Printf, Measures\n",
    "\n",
    "theme(:juno)\n",
    "\n",
    "# equity / cash balance\n",
    "p1 = plot()\n",
    "Fastback.plot_balance!(p1, balance_data; title=\"Account\")\n",
    "Fastback.plot_equity!(p1, equity_data)\n",
    "\n",
    "# drawdowns\n",
    "p2 = Fastback.plot_drawdown(drawdown_data)\n",
    "\n",
    "# stocks performance\n",
    "p3 = plot(\n",
    "    df.dt_close, df[!, 2] ./ df[1, 2];\n",
    "    title=\"Stocks performance (normalized)\",\n",
    "    yformatter=y -> @sprintf(\"%.1f\", y),\n",
    "    label=names(df)[2],\n",
    "    linetype=:steppost,\n",
    "    color=:green);\n",
    "for i in 3:ncol(df)\n",
    "    plot!(p3,\n",
    "        df.dt_close, df[!, i] ./ df[1, i];\n",
    "        label=names(df)[i])\n",
    "end\n",
    "\n",
    "# P&L breakdown by stocks\n",
    "pnl_by_inst = acc.trades |>\n",
    "              @groupby(_.order.inst.symbol) |>\n",
    "              @map({\n",
    "                  symbol = key(_),\n",
    "                  pnl = sum(getfield.(_, :realized_pnl_settle))\n",
    "              }) |> DataFrame\n",
    "p4 = bar(string.(pnl_by_inst.symbol), pnl_by_inst.pnl;\n",
    "    legend=false,\n",
    "    title=\"P&L breakdown by stocks [USD]\",\n",
    "    permute=(:x, :y),\n",
    "    xlims=(0, size(pnl_by_inst)[1]),\n",
    "    yformatter=y -> @sprintf(\"%.0f\", y),\n",
    "    color=\"#BBBB00\",\n",
    "    linecolor=nothing,\n",
    "    bar_width=0.5)\n",
    "\n",
    "plot(p1, p2, p3, p4;\n",
    "    layout=@layout[a{0.4h}; b{0.15h}; c{0.3h}; d{0.15h}],\n",
    "    size=(800, 800), margin=0mm, left_margin=5mm);"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Calculate statistics per stock"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Calculates summary statistics for each stock. Using the `Query.jl` package,\n",
    "all trades are first grouped by instrument symbol, then average P&L,\n",
    "worst P&L, best P&L, and win rate are calculated.\n",
    "Finally, the results are piped into a `DataFrame` and printed\n",
    "using the `PrettyTables.jl` package."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "┌────────┬─────────┬───────────┬──────────┬──────────┐\n",
      "│ Symbol │ Avg P&L │ Worst P&L │ Best P&L │ Win Rate │\n",
      "├────────┼─────────┼───────────┼──────────┼──────────┤\n",
      "│     GE │ 40.5742 │  -109.494 │  586.775 │     0.42 │\n",
      "│   AAPL │ 28.9128 │  -133.464 │  168.848 │     0.75 │\n",
      "│   NVDA │ 50.9232 │  -116.916 │  829.174 │     0.62 │\n",
      "│   TSLA │ 63.3804 │  -234.683 │   555.35 │     0.56 │\n",
      "└────────┴─────────┴───────────┴──────────┴──────────┘\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "using PrettyTables\n",
    "\n",
    "df = acc.trades |>\n",
    "@groupby(_.order.inst.symbol) |>\n",
    "@map({\n",
    "    symbol = key(_),\n",
    "    avg_pnl = sum(getfield.(_, :realized_pnl_settle)) / length(_),\n",
    "    worst_pnl = minimum(getfield.(_, :realized_pnl_settle)),\n",
    "    best_pnl = maximum(getfield.(_, :realized_pnl_settle)),\n",
    "    win_rate = round.(count(getfield.(_, :realized_pnl_settle) .> 0) / count(is_realizing.(_)), sigdigits=2),\n",
    "}) |> DataFrame\n",
    "\n",
    "pretty_table(df; column_labels=[\"Symbol\", \"Avg P&L\", \"Worst P&L\", \"Best P&L\", \"Win Rate\"])"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.10"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.10",
   "language": "julia"
  }
 },
 "nbformat": 4
}
